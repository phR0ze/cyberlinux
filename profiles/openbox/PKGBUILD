# MIT License
# Copyright (c) 2017-2021 phR0ze
# 
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
# 
# The above copyright notice and this permission notice shall be included in all
# copies or substantial portions of the Software.
# 
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

#-------------------------------------------------------------------------------
# Maintainer: phR0ze
#-------------------------------------------------------------------------------
pkgbase=cyberlinux-openbox
pkgname=(
    "${pkgbase}-lite"
    "${pkgbase}-server"
    "${pkgbase}-netbook"
    "${pkgbase}-desktop"
    "${pkgbase}-theater"
    "${pkgbase}-laptop"
    "${pkgbase}-live"
)
pkgver=0
pkgrel=1
pkgdesc='Configuration packages for the cyberlinux openbox profile'
arch=('any')
license=('MIT')
url="https://github.com/phR0ze/cyberlinux/blob/master/profiles/openbox"

# Configure environment
shopt -s dotglob
shopt -s extglob
_none="\e[m"
_cyan="\e[1;36m"

pkgver()
{
  cat "${startdir}/../VERSION"
}

# Because were running the makepkg in a container at a specific location
# the staging location is /home/build/PKG
pkg()
{
  local name=$1                       # e.g. core
  install=$name.install               # Specific installer script for deployment package
  local configs="${startdir}/${name}" # /home/build/profiles/openbox/core i.e. inside container

  if [ -d "${configs}" ]; then
    msg "${_cyan}Installing '${name}' configuration files${_none}"
    cp -a "$configs"/* "${pkgdir}"
  fi
}

package_cyberlinux-openbox-lite()
{
  pkgdesc="Lite openbox system - a building block specialized openbox deployments"
  depends=(
    "cyberlinux-standard-x11>=${pkgver}"

    # LXDE
    'gsimplecal'                # Simple calendar like windows systray calendar
    'gtk2fontsel'               # Simple font viewer
    'lxappearance'              # Feature-rich GTK+ theme switcher of the LXDE Desktop, group: lxde
    'lxappearance-obconf'       # Plugin for LXAppearance to configure Openbox, group: lxde
    'lxde-common'               # Common files of the LXDE Desktop, group: lxde
    'lxinput'                   # Keyboard and mouse configuration utility, group: lxde
    'lxrandr'                   # Monitor configuration tool, group: lxde
    'lxsession'                 # Enable .desktop files in ~/.config/autostart, /etc/xdg/autostart, group: lxde
    'lxterminal'                # Terminal for LXDE, alt: terminator, group: lxde
    'nitrogen'                  # Lightweight wallpaper manager
    'pnmixer'                   # Volume controls in system tray, repo: cyberlinux
    'openbox'                   # Lightweight X11 window manager, group: lxde
    'tint2'                     # Lightweight taskmanager and system tray
  ) 
  pkg lite
}

package_cyberlinux-openbox-server()
{
  pkgdesc="Openbox server apps including restricted personal use only software"
  depends=(
    "cyberlinux-standard-server>=${pkgver}"
    "cyberlinux-openbox-lite>=${pkgver}"

    # Restricted licensing to personal use only
    'teamviewer'                # Excellent remoting software
  )
  pkg server
}

package_cyberlinux-openbox-netbook()
{
  pkgdesc="Openbox netbook built on the lite package adding productivity apps"
  depends=(
    "cyberlinux-standard-netbook>=${pkgver}"
    "cyberlinux-openbox-lite>=${pkgver}"
  )
  pkg netbook
}

package_cyberlinux-openbox-desktop()
{
  pkgdesc="Full openbox system with media and productivity apps including restricted personal use only software"
  depends=(
    "cyberlinux-standard-desktop>=${pkgver}"
    "cyberlinux-openbox-netbook>=${pkgver}"

    'cyberlinux-plank'          # Light weight, simple clean app dock
    'guake'                     # Drop-down python based guake style terminal
    'idesk'                     # Lightweight desktop icons for X

    # Restricted licensing to personal use only
    'teamviewer'                # Excellent remoting software
    'ttf-ms-fonts'              # Core TTF Fonts from Microsoft
  )
  pkg desktop
}

package_cyberlinux-openbox-theater()
{
  pkgdesc="Theater system - a lean back entertainment experience"
  depends=(
    "cyberlinux-openbox-desktop>=${pkgver}"
  )
  pkg theater
}

package_cyberlinux-openbox-laptop()
{
  pkgdesc="Laptop system - built on top of desktop with laptop configuration"
  depends=(
    "cyberlinux-openbox-desktop>=${pkgver}"
  )
  pkg laptop
}

package_cyberlinux-openbox-live()
{
  pkgdesc="Live system - built on top of desktop for recovery"
  depends=(
    "cyberlinux-openbox-desktop>=${pkgver}"
  )
  pkg live
}

# vim: ts=2:sw=2:sts=2
