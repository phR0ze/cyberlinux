#MIT License
#Copyright (c) 2017 phR0ze
#
#Permission is hereby granted, free of charge, to any person obtaining a copy
#of this software and associated documentation files (the "Software"), to deal
#in the Software without restriction, including without limitation the rights
#to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
#copies of the Software, and to permit persons to whom the Software is
#furnished to do so, subject to the following conditions:
#
#The above copyright notice and this permission notice shall be included in all
#copies or substantial portions of the Software.
#
#THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
#IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
#FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
#AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
#LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
#OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
#SOFTWARE.

#-------------------------------------------------------------------------------
# Maintained by phR0ze
#-------------------------------------------------------------------------------
pkgname=openvpn-pia
pkgver=0.0.9
pkgrel=1
pkgdesc="Private Internet Access OpenVPN automation and UI"
arch=('any')
url="https://github.com/phR0ze/openvpn-pia"
license=('MIT')
depends=('ruby-gtk3' 'ruby-cmds' 'openvpn' 'systemd')
makedepends=(inkscape)
install=$pkgname.install

source=("git+https://github.com/phR0ze/$pkgname.git#tag=$pkgver"
        "https://www.privateinternetaccess.com/openvpn/openvpn-ip.zip"
        "https://www.privateinternetaccess.com/openvpn/openvpn-strong.zip"
        "openvpn-pia.desktop")

sha256sums=('SKIP'
            '8d7d75b7cc7b5a40dc26f778eb646c348011657bba90e137a359d18332c4bdad'
            '4897c3a8dd988e87c4c53db2dd1522a0a7112d58a0357425c09d1ce5f9f5a1b5'
            'd8eec410a02854ba4dd392e48a56129764ebd5b7307296d8d3dd968ebdcb0721')

build()
{
  cd $srcdir
  msg "Building $pkgname..."

  # Rename PIA configuration files
  # openvpn doesn't do well with spaces
  for ovpn in $srcdir/*.ovpn; do
    dir=$(dirname "$ovpn")
    base=$(basename "$ovpn")
    x="${base}"
    x="${x// /-}"
    x="${x,,}"
    if [ "${x}" != "${base}" ]; then
      msg2 "Renaming: $base => $x"
      mv "$ovpn" "$dir/$x"
    fi
  done

  # Generate install icons from scalables
  for svg in $srcdir/$pkgname/images/*.svg; do
    base=$(basename "$svg")
    x="${base//svg/png}"
    if [ ! -e $srcdir/$x ]; then
      msg2 "Generating icon $x"
      inkscape $svg --export-png=$srcdir/$x --export-width=32
    fi
  done
}

package()
{
  cd $srcdir
  msg "Packaging $pkgname..."

  # Package license and documentation
  msg2 "Packaging license and docs..."
  install -dm750 $pkgdir/usr/share/doc/$pkgname
  install -Dm644 $srcdir/$pkgname/LICENSE -t $pkgdir/usr/share/doc/$pkgname
  install -Dm644 $srcdir/$pkgname/README.md -t $pkgdir/usr/share/doc/$pkgname

  # Package data files
  msg2 "Packaging data files"
  install -Dm644 $srcdir/$pkgname/$pkgname-settings.ui -t $pkgdir/usr/share/$pkgname
  for x in $srcdir/$pkgname/images/*.png; do
    msg2 "Packaging images: $x"
    install -Dm644 $x -t $pkgdir/usr/share/$pkgname/images
  done

  # Package executables
  msg2 "Packaging executables"
  install -Dm755 $srcdir/$pkgname/$pkgname -t $pkgdir/usr/bin
  install -Dm755 $srcdir/$pkgname/$pkgname-cli -t $pkgdir/usr/bin

  # Package generated icons
  for x in $srcdir/$pkgname*.png; do
    msg2 "Packaging generated icon: $x"
    install -Dm644 $x -t $pkgdir/usr/share/icons/hicolor/32x32/apps
    rm $x
  done

  # Package launch files
  msg2 "Packaging desktop launch file"
  install -Dm644 $pkgname.desktop -t $pkgdir/usr/share/applications

  # Package PIA configuration
  install -dm750 $pkgdir/etc/openvpn/client
  for x in $srcdir/*.ovpn $srcdir/*.crt $srcdir/*.pem; do
    msg2 "Packaging pia config file: $x"
    install -Dm600 -t $pkgdir/etc/openvpn/client "$x"
  done
}

# vim: ts=2:sw=2:sts=2
